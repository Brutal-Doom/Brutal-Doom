//-----======_ACTORS_======-----]
//------------------------------]
//---=====---


/////////////////////////////////////////
// First Person Executions
/////////////////////////////////////////

ACTOR ExecutionWeapon : Weapon//BrutalWeapon
{
	//VisibleToPlayerClass Purist
	+WEAPON.CHEATNOTWEAPON
	+INVENTORY.UNDROPPABLE
	States
	{
	Select:
		TNT1 A 1 A_Raise
		Wait

	Deselect:
		TNT1 A 1 A_Lower
		Wait

	Ready:
		TNT1 A 0 A_GiveInventory("IsExecutingEnemy",1)
	DoExecution:
		TNT1 A 0 A_SetPitch(65)
		TNT1 A 1
		TNT1 A 0 A_ChangeFLag("NOPAIN", 1)
		TNT1 A 0 A_ChangeFLag("PUSHABLE", 0)
		TNT1 A 0 A_Stop
		TNT1 A 0 SetPlayerProperty(0,1,4)
		KICK A 0 A_Takeinventory("Kicking",1)
		TNT1 A 0 A_JumpIfInventory("IsFloating", 1, "ExecuteAirStomp")
		TNT1 A 0 A_JumpIfInventory("ExecuteBashWall", 1, "BashWall")
		TNT1 A 0 A_JumpIfInventory("ExecuteStompWall", 1, "StompWall")
		TNT1 AAAAA 1 A_SetPitch(90)
		TNT1 A 0 A_Jump(128, "Execution2")
		Goto Execution1

	Stompwall:
		TNT1 A 0 A_Stop
		//TTNT1 A 0 A_JumpIfInventory("IsFloating", 1, "ExecuteAirStomp")
		TNT1 A 0 A_SetPItch(10)
		TNT1 A 0 A_Recoil(-1)
		KICK JKLMN 1 A_Recoil(-1)
		RIFF A 0 A_FireCustomMissile("ExecutionAttackStompWall", 0, 0, 0, -7)
		KICK OOO 2  A_Recoil(-1)
		KICK A 0 A_Takeinventory("Kicking",1)
		KICK PQRST 1
		KICK A 0 A_Takeinventory("ExecuteStompWall",1)
		KICK A 0 A_Takeinventory("ExecuteBashWall",1)
		TNT1 A 5
		Goto FinishExecution+8

	Bashwall:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("imp/pain", 4)
		TNT1 A 0 A_SetPItch(10)
		TNT1 A 0 A_ZoomFactor(1.3)
		IEHW A 2
		TNT1 A 0 A_ZoomFactor(1.0)
		IEHW A 8
		IEHW AAA 1 A_SetAngle(angle+6)
		IEHW BCDEF 1 A_SetAngle(angle+6)
		IEHW G 3
		TNT1 AA 0 A_CustomMissile("XDeath1", 48, 0, 45, 2)
		TNT1 A 0 A_PlaySound("imp/pain", 4)
		TNT1 A 0 A_CustomMissile("KickAttackFleshy", 48, 0, 45, 2)
		IEHW HIJKKK 1 A_SetAngle(angle-3)
		IEHW KKKJIH 1 A_SetAngle(angle+3)
		IEHW FG 3
		TNT1 AA 0 A_CustomMissile("XDeath1", 48, 0, 45, 2)
		TNT1 A 0 A_PlaySound("imp/pain", 4)
		TNT1 A 0 A_CustomMissile("KickAttackFleshy", 48, 0, 45, 2)
		IEHW HIJKKK 1 A_SetAngle(angle-3)
		IEHW KKKJIH 1 A_SetAngle(angle+3)
		IEHW FG 3
		TNT1 AA 0 A_CustomMissile("XDeath1", 48, 0, 45, 2)
		TNT1 A 0 A_PlaySound("imp/pain", 4)
		TNT1 A 0 A_CustomMissile("KickAttackFleshy", 48, 0, 45, 2)
		IEHW HIJKKK 1 A_SetAngle(angle-3)
		IEHW KKKJIH 1 A_SetAngle(angle+3)
		TNT1 AA 0 A_CustomMissile("XDeath1", 48, 0, 45, 2)
		TNT1 A 0 A_CustomMissile("ImpHeadExplode", 48, 0, 45, 2)
		TNT1 A 0 A_SpawnItemEx("ArmorShardFatalityBonus2", 0, 0, 20, random (-1, 1), random (-1, 1), 8)
		TNT1 A 0 A_CustomMissile("XDeath2", 48, 0, 45, 2)
		TNT1 AAA 0 A_CustomMissile("KickAttackFleshy", 48, 0, 45, 2)
		IEHW LMNO 1
		IEHW O 6
		IEHW PQRST 2 A_SetAngle(angle-4)
		TNT1 A 0 A_SpawnItem ("GrowingBloodPool")
		TNT1 A 0 A_SpawnItem("DeadImp_NoHead")
		TNT1 AAA 1 A_SetAngle(angle-3)
		KICK A 0 A_Takeinventory("Kicking",1)
		KICK A 0 A_Takeinventory("ExecuteBashWall",1)
		KICK A 0 A_Takeinventory("ExecuteStompWall",1)
		TNT1 A 5
		Goto FinishExecution+8

	ExecuteAirStomp:
		TNT1 A 0
		TNT1 A 0 A_Stop
		TNT1 A 0 A_GiveInventory("IsExecutingOnAir", 1)
		TNT1 A 0 ThrustThingZ(0, 10, 0, 1)
		3TOM A 0 A_JumpIfInventory("BootsSmearedWithGreenBlood", 1, 4)
		2TOM A 0 A_JumpIfInventory("BootsSmearedWithBlueBlood", 1, 3)
		1TOM A 0 A_JumpIfInventory("BootsSmearedWithRedBlood", 1, 2)
		STOM A 0
		"####" "#" 0
		//"####" ABCCCCC 1 A_SetPitch(80)
		"####" A 1 offset(0, 66) A_SetPitch(80)
		"####" A 1 offset(0, 48) A_SetPitch(80)
		"####" A 1 offset(0, 36) A_SetPitch(80)
		"####" A 1 offset(0, 32) A_SetPitch(80)
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_CheckFloor("DoAirStomp")
	DoAirStomp:
		RIFF AAA 0 A_FireCustomMissile("ExecutionAttackKick", 0, 0, 0, 0)
		TNT1 A 0 A_TakeInventory("BootsSmearedWithGreenBlood", 20)
		TNT1 A 0 A_TakeInventory("BootsSmearedWithBlueBlood", 20)
		TNT1 A 0 A_GiveInventory("BootsSmearedWithRedBlood", 15)
		STOM B 2 offset(0,34) A_Quake(4,5,0,12,"")
		STOM B 4 offset(0,32)
		STOM B 0 ThrustThingZ(0, 20, 1, 1)
		STOM B 0 A_TakeInventory("IsExecutingOnAir", 1)
		STOM B 0 A_Takeinventory("ExecuteStompWall",1)
		STOM B 0 A_Takeinventory("ExecuteBashWall",1)
		Goto FinishExecution

	Execution1:
		TNT1 A 0 A_PlaySound("Kick/Swing", 5)
		TNT1 A 0 SetPlayerProperty(0,1,0)
		3ICK A 0 A_JumpIfInventory("BootsSmearedWithGreenBlood", 1, 4)
		2ICK A 0 A_JumpIfInventory("BootsSmearedWithBlueBlood", 1, 3)
		1ICK A 0 A_JumpIfInventory("BootsSmearedWithRedBlood", 1, 2)
		KICK A 0
		"####" ABC 1 A_SetPitch(pitch + 16)
		//TTNT1 A 0 A_JumpIfInventory("IsFloating", 1, "ExecuteAirStomp")
		"####" A 0 A_Quake(3,4,0,12,"")
		"####" D 1 A_SetAngle(angle + 4) 
		"####" D 0 A_FireCustomMissile("ExecutionAttackKick", 0, 0, 0, -7)
		"####" A 0 A_Takeinventory("Kicking",1)
		TNT1 A 0 A_TakeInventory("BootsSmearedWithGreenBlood", 20)
		TNT1 A 0 A_TakeInventory("BootsSmearedWithBlueBlood", 20)
		TNT1 A 0 A_GiveInventory("BootsSmearedWithRedBlood", 15)
		1ICK EEEE 1//FGHIJKLM 1
		1ICK E 0 SetPlayerProperty(0,0,0)
		1ICK E 5
		Goto FinishExecution

	Execution2:
		TNT1 A 0
		TNT1 A 0 A_ZoomFactor(1.1)
		4ONG A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		PONG A 0
		"####" A 0 A_PlaySound("skeleton/swing")
		"####" BCD 1 A_Stop
		"####" E 3 A_FireCustomMissile("ExecutionAttackMelee", 0, 0, 0, 0)
		"####" FFGCB 1 A_Stop
		"####" A 0 A_PlaySound("skeleton/swing")
		"####" VWX 1 A_Stop
		"####" Y 3 A_FireCustomMissile("ExecutionAttackMelee", 0, 0, 0, 0)
		"####" ZZ 1 A_Stop
		"####" "[" 1 A_Stop
		"####" WV 1 A_Stop
		"####" A 0 A_PlaySound("skeleton/swing")
		"####" BCD 1 A_Stop
		"####" E 3 A_FireCustomMissile("ExecutionAttackMelee", 0, 0, 0, 0)
		"####" FFGCB 1 A_Stop
		TNT1 A 2
		TNT1 A 2 A_PlaySound("*grunt", 5)
		4UNG A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		PUNG A 0
		"####" A 1 A_PlaySound("skeleton/swing")
		TNT1 A 0 A_TakeInventory("PowerGreenBloodOnVisor", 1)
		TNT1 A 0 A_TakeInventory("PowerBlueBloodOnVisor", 1)
		TNT1 A 0 A_GiveInventory("PowerBloodOnVisor", 1)
		PUNG BC 1 A_FireCustomMissile("ExecutionAttackKick", 0, 0, 0, 0)
		4UNG A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		1UNG A 0
		"####" DEFFGGHIJ 1 A_Stop
		TNT1 A 5 A_ZoomFactor(1.0)
		Goto FinishExecution

	Execution3:
		KIKF ABC 1
		//TTNT1 A 0 A_JumpIfInventory("IsFloating", 1, "ExecuteAirStomp")
		TNT1 A 0 A_PlaySound("Kick/Swing")
		KIKF D 1
		TNT1 AAA 0 A_FireCustomMissile("ExecutionAttackFatality", 0, 0, 0, 0)
		KIKF DDDDDDD 1 A_SetPitch(Pitch-10)
		KIKF ECBA 2
		TNT1 A 0 SetPlayerProperty(0,0,4)
		TNT1 A 0 A_ChangeFLag("NOPAIN", 0)
		TNT1 A 0 A_ChangeFLag("PUSHABLE", 1)
		TNT1 A 5
		KICK A 0 A_Takeinventory("ExecuteDownedEnemy",1)
		TNT1 A 0 A_takeInventory("PressedUSe", 1)
		//Goto GoingToReady
	FinishExecution:
		"####" "#" 0
		"####" "#" 1 offset(0,42) A_SetPitch(Pitch-5)
		"####" "#" 1 offset(0,56) A_SetPitch(Pitch-5)
		"####" "#" 1 offset(0,88) A_SetPitch(Pitch-10)
		"####" "#" 1 offset(0,110) A_SetPitch(Pitch-10)
		"####" "#" 1 offset(0,138) A_SetPitch(Pitch-15)
		TNT1 A 1 offset(0,150) A_SetPitch(Pitch-15)
		TNT1 AA 1 offset(0,32) A_SetPitch(Pitch-5)
		TNT1 A 1 A_SetPitch(0)
		TNT1 A 0 A_ChangeFLag("NOPAIN", 0)
		TNT1 A 0 A_ChangeFLag("PUSHABLE", 1)
		TNT1 A 0 SetPlayerProperty(0,0,4)
		KICK A 0 A_Takeinventory("ExecuteDownedEnemy",1)
		TNT1 A 0 A_takeInventory("PressedUSe", 1)
		//Goto GoingToReady
	Invisiloop:
		TNT1 A 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_TakeInventory("IsExecutingEnemy",1)
		TNT1 A 0 A_TakeInventory("BDWEaponACtion", 1)
		TNT1 A 0 A_TakeInventory("ExecuteDownedEnemy", 1)
		TNT1 A 0 A_GiveInventory("FatalityTokenCleaner", 1)
		TNT1 A 0 A_TakeInventory("Kicking",1)
		TNT1 A 0 A_TakeInventory("Salute1", 1)
		TNT1 A 0 A_TakeInventory("Salute2", 1)
		TNT1 A 0 A_TakeInventory("Taunting",1)
		TNT1 A 0 A_TakeInventory("TossGrenade",1)
		TNT1 A 0 A_TakeInventory("IsRunning",1)
		TNT1 A 0 A_TakeInventory("Reloading",1)
	Fire:
		TNT1 A 1
		Loop
	}
}