ACTOR Revolver : BrutalWeapon 5874
{
	//$category "Weapons"
	Weapon.AmmoType1 "AmmoShell"
	Weapon.AmmoType2 "RevolverAmmo"
	Weapon.SelectionOrder 1600
	Inventory.PickupSound "Revolver/Hammer"
	Inventory.PickupMessage "$GOTREVOLVER"
	Obituary "$OBBD_REVOLVER"
	Tag "$TAGBD_REVOLVER"
	States
	{
	Inspect:
		REVO A 0 A_JumpIfInventory("DoInspect",1,"ContinueInspect")
		REVO A 0 A_JumpIfInventory("FiredSecondary",1,"ContinueInspect")
		REVO A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		REVO A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		REVO A 0 A_JumpIfInventory("Reloading",1,"Reload")
		REVO A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		REVO A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		REVO A 0 A_JumpIfInventory("Unloading",1,"Unload")
		REVO A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		REVO A 6 A_WeaponReady(WRF_ALLOWZOOM)
		REVO A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		REVO A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		REVO A 0 A_GiveInventory("DoInspect",1)
		REVO A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		REVO A 0 A_TakeInventory("DoInspect",1)
		Goto Ready3

	ContinueInspect:
		TNT1 AA 0
		REVO A 0 A_TakeInventory("DoInspect",1)
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("Reloading",1)
		REVR ABCDEF 1 A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		TNT1 A 0 A_PLaySound("Revolver/Open", 5)
		REVR GHIJ 1 A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		REVR K 1 A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		REVR K 15 A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		TNT1 A 0 A_GiveInventory("HoldTheRevolver",1)
	Inspect2:
		REVR K 2 A_WeaponReady(WRF_NOBOB|WRF_ALLOWRELOAD|WRF_NOFIRE)
		TNT1 A 0 A_TakeInventory("HoldTheRevolver",1)
		REVR JIH 1 A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		TNT1 A 0 A_PLaySound("Revolver/Close", 5)
		REVR GFEDCBA 1 A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory("Reloading",1)
		Goto Ready

	KickFlash:
		REVO XYZ 1
		REVO "[" 1
		REVO "\" 10
		REVO "[" 1
		REVO ZYX 1
		stop
	SlideFlash:
		REVO XYZ 1
		REVO "\" 35
		stop
	SlideFlashEnd: 
		REVO "\" 6
		REVO "[" 1
		REVO ZYX 1
		stop
	SlideFlashEndEarly: 
		REVO "\" 2
		REVO "[" 1
		REVO ZYX 1
		stop

	Spawn:
		REVP A -1
		Stop

	Deselect:
		REVO A 0
		REVO A 0 A_Takeinventory("Zoomed",1)
		REVO A 0 A_TakeInventory("TossGrenade", 1)
		TNT1 A 0 A_TakeInventory("HoldTheRevolver",1)
		REVO A 0 A_Takeinventory("ADSmode",1)
		REVO A 0 A_Takeinventory("UseShotgunStrap",1)
		REVO A 0 A_ZoomFactor(1.0)
		REVS EFGH 1
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait

	DeselectForChangingAmmo:
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait

	Select:
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_GiveInventory("TokenCleaner")
		TNT1 A 0 A_Giveinventory("ShotgunSelected",1)
		TNT1 A 0 A_SetCrosshair( CallACS("GetCrosshair", 20) )
		TNT1 A 0 A_GunFlash
		TNT1 A 1
	SelectAnimation:
		TNT1 A 0 A_JumpIfInventory("NeedToHammer",1,"SelectHammering")
		TNT1 A 0 A_PlaySound("Revolver/Hammer", 5)
		REVS ABCD 1 A_WeaponReady(WRF_NOFIRE)
		DEAG A 0 A_GunFlash
		TNT1 A 0 A_TakeInventory("Reloading",1)
		Goto Ready3

	SelectHammering:
		TNT1 A 0 A_PlaySound("Revolver/Hammer", 5)
		TNT1 A 0 A_TakeInventory("NeedToHammer",1)
		REVS IJKL 1 A_WeaponReady(WRF_NOFIRE)
		DEAG A 0 A_GunFlash
		TNT1 A 0 A_TakeInventory("Reloading",1)
		Goto Ready3

	Ready3:
	Ready:
		REVO A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		REVO A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		REVO A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		REVO A 0 A_JumpIfInventory("Reloading",1,"Reload")
		REVO A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		REVO A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		REVO A 0 A_JumpIfInventory("Unloading",1,"Unload")
		REVO A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
	OkToFire:
		REVO K 0 A_JumpIfInventory("NeedToHammer",1,2)
		REVO A 0
		REVO "#" 1 A_WeaponReady//(WRF_ALLOWRELOAD)
		REVO A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		REVO A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		Goto Ready

	Fire:
		REVO A 0 A_Takeinventory("Reloading",1)
		REVO A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"NoReload")
		REVO A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		REVO A 0 A_JumpIfInventory("RevolverAmmo",1,1)
		Goto NoAmmo
		TNT1 A 0 A_JumpIfInventory("NeedToHammer",1,"Hammering")
		REVO A 0 A_Takeinventory("RevolverAmmo",1)
		Goto ResumeFireFrame

	NoReload:
		REVO A 0 A_JumpIfInventory("AmmoShell",1,1)
		Goto NoAmmo
		REVO A 0 A_Takeinventory("AmmoShell",1,TIF_NOTAKEINFINITE)
	ResumeFireFrame:
		TNT1 AAAA 0
		REVO A 0 A_ZoomFactor(0.9)
		REVO A 0 A_AlertMonsters
		REVO A 0 A_PlaySound("Revolver/Fire", 1)
		RIFF A 0 A_FireCustomMissile("DistantFireSoundShotgun", random(-1,1), 0, 0, -12, 0, random(-1,1))
		REVO A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		TNT1 AAAAAAAAAAAAAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles", random(-12,12), 0, -1, 0, 0, random(-9,9))
		TNT1 AAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles2", random(-12,12), 0, -1, 0, 0, random(-9,9))
		TNT1 AAAAA 0 A_FireCustomMissile("DecorativeTracer", random(-3,3), 0, 0, -12, 0, random(-3,3))
		//SHO9 A 0 A_firebullets (0,0,1,40,"ShotgunPuff",FBF_NORANDOM,300)
		//REVO A 0 A_FireBullets (20, 7, 7, 22, "ShotgunPuff", FBF_NORANDOM)  //Revopuff is the og
		//REVO A 0 A_FireBullets (3, 3, 2, 22, "ShotgunPuff", FBF_NORANDOM)
		REVO B 1 BRIGHT A_FireBullets (20, 7, 7, 22, "ShotgunPuff", FBF_NORANDOM)  //Revopuff is the og
		REVO C 1 BRIGHT A_FireBullets (3, 3, 2, 22, "ShotgunPuff", FBF_NORANDOM)
		REVO A 0 A_JumpIfInventory("IsNOTTacticalClass", 1, 3)//Tactical Mode deals extra damage
		REVO A 0 A_FireBullets (12, 3, 5, 22, "ShotgunPuff2", FBF_NORANDOM)
		REVO A 0 A_SetPitch(pitch-10)
		//REVO BC 1 BRIGHT
		REVO A 0 A_SetPitch(-6.0 + pitch)
		REVO A 0 A_ZoomFactor(1.00)
		REVO D 1 A_SetPitch(+1.0 + pitch)
		REVO EF 1 A_SetPitch(+1.0 + pitch)
		REVO G 1 A_SetPitch(+0.5 + pitch)
		REVO HIJ 1 A_SetPitch(pitch+0.5)
		REVO K 3 A_GiveInventory("NeedToHammer",1)
		REVO A 0 A_JumpIfInventory("RevolverAmmo",1,2)
		Goto Ready3

	Hammering:
		REVO LMNOP 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("Revolver/Hammer", 5)
		REVO QRSTUVW 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		REVO A 0 A_Takeinventory("NeedToHammer",1)
		Goto Ready3

	StartSprint:
		REVO XYZ  1 A_WeaponReady(WRF_NOFIRE)
		REVO A 0 A_Takeinventory("Zoomed",1)
		REVO A 0 A_Takeinventory("ADSmode",1)
		REVO A 0 A_ZoomFactor(1.0)
		REVO A 0 A_JumpIfInventory("UsedStamina", 40, "StopSprintTired")
	Sprinting:
		REVP B 0 offset(-9,32)
		//REVO A 0 offset(-9,32) A_JumpIfInventory("IsStandingStill", 1, "Ready3")
		REVO A 0 offset(-9,32) A_JumpIfInventory("UsedStamina", 100, "StopSprintTired")
		PLAY A 0 offset(-9,32) ACS_ExecuteAlways(852, 0, 0, 0, 0)//Makes player faster.
		REVO A 0 offset(-9,32) A_JumpIfInventory("PowerStrength", 1, 2)
		REVO A 0 offset(-9,32) A_GiveInventory("UsedStamina", 3)
		REVO A 0 offset(-9,32)
		REVO "[" 1 offset(-9,34) A_SetPitch(pitch -0.5)
		REVO "[" 1 offset(-6,36) A_SetPitch(pitch -0.5)
		REVO "[" 1 offset(-3,38) A_SetPitch(pitch -0.5)
		REVO "[" 1 offset(0,38) A_SetPitch(pitch -0.5)
		REVO "[" 1 offset(3,36) A_SetPitch(pitch -0.5)
		REVO "[" 1 offset(6,34) A_SetPitch(pitch -0.5)
		REVO "[" 1 offset(9,32) A_SetPitch(pitch -0.5)
		REVO A 0 offset(9,32) A_WeaponReady(WRF_NOBOB)
		REVO A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		REVO A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		REVO A 0 A_JumpIfInventory("Reloading",1,"Reload")
		REVO A 0 offset(-9,32) A_GiveInventory("UsedStamina", 3)
		REVO A 0 offset(9,33) A_SpawnItemEx("FootStep", 0, 0, 2, 0, 0, -4)
		REVO "[" 1 offset(9,34) A_SetPitch(pitch +0.5)
		REVO "[" 1 offset(6,36) A_SetPitch(pitch +0.5)
		REVO "[" 1 offset(3,38) A_SetPitch(pitch +0.5)
		REVO "[" 1 offset(0,38) A_SetPitch(pitch +0.5)
		REVO "[" 1 offset(-3,36) A_SetPitch(pitch +0.5)
		REVO "[" 1 offset(-6,34) A_SetPitch(pitch +0.5)
		REVO "[" 1 offset(-9,32) A_SetPitch(pitch +0.5)
		REVO A 0 offset(-9,32) A_WeaponReady(WRF_NOBOB)
		REVO A 0 offset(-9,32) A_JumpIfInventory("IsRunning", 1, "Sprinting")
		Goto StopSprint

	StopSprintTired:
		REVO ZYX 1
		PLAY A 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		REVO A 0 A_PlaySound("Tired", 2)
		REVO A 0 A_TakeInventory("UsedStamina", 2)
		REVO A 5 A_WeaponReady
		REVO A 0 A_TakeInventory("UsedStamina", 2)
		REVO A 5 A_WeaponReady
		REVO A 0 A_TakeInventory("UsedStamina", 2)
		REVO A 5 A_WeaponReady
		REVO A 0 A_TakeInventory("UsedStamina", 2)
		REVO A 5 A_WeaponReady
		REVO A 0 A_TakeInventory("UsedStamina", 2)
		REVO A 5 A_WeaponReady
		Goto Ready

	StopSprint:
		REVO A 0 A_JumpIfInventory("UsedStamina", 60, "StopSprintTired")
		REVO ZYX 1
		PLAY A 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		Goto Ready

	ReloadEmpty:
		REVU ST 1
		REVU KKKKKK 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		REVU LMMN 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("Shotgun/Reload", 5)
		Goto InsertBullets

	ReloadWithNoAmmoLeft:
	Reload:
		REVO A 0
		REVO A 0 A_TakeInventory("Reloading", 1)
		REVO A 0 A_JumpIfInventory("AmmoShell", 1, 2)
		Goto NoAmmo
		REVO A 0
		REVO A 0 A_Takeinventory("Zoomed",1)
		REVO A 0 A_ZoomFactor(1.0)
		REVO A 0 A_JumpIfInventory("HoldTheRevolver",1,"Inspect2")
		REVO A 0 A_JumpIfInventory("RevolverAmmo",0,"Inspect")
		REVO A 0 A_JumpIfInventory("HasUnloadedRevolver", 1, "ReloadUnloaded")
		REVO A 0 A_JumpIfInventory("TurboReload", 1, "TurboReload")//Check if reloads are disabled.
		REVR B 0 A_GiveInventory ("Pumping", 1)
		REVR ABCDEFF 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		TNT1 A 0 A_PLaySound("Revolver/Open", 5)
		REVR GHIJKKK 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		REVR LMNOP 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		//REVO A 0 A_JumpIfInventory("RevolverAmmo",1,2)
		//Goto ReloadEmpty
		REVR Q 2 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
	InsertingShells:
		REVO A 0
		REVO A 0 A_JumpIfInventory("AmmoShell",1,2)//if there are no more reserve shells, finish reload.
		Goto FinishedInsertingShells
		REVO A 0
		REVO A 0 A_GiveInventory ("Pumping", 1)
		SSHR A 0 A_TakeInventory("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory("HasUnloadedRevolver",1,3)
		TNT1 A 0 A_FireCustomMissile("SSGCaseSpawner",0,0,-10, -20)
		REVO A 0 A_JumpIfInventory("RevolverAmmo",0,"FinishedInsertingShells")
		REVO A 0 A_Giveinventory("RevolverAmmo",1)
		REVO A 0 A_JumpIfInventory("NoReloading",1,2)
		REVO A 0 A_Takeinventory("AmmoShell",1,TIF_NOTAKEINFINITE)
	InsertShellAnimation:
		REVO A 0
		REVR Z 2
		REVR RS 1
		TNT1 A 0 A_PlaySound("Shotgun/Reload", 5)
		REVR TT 1
		REVR UVWXY 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		SSHR A 0 A_JumpIfInventory("Reloading", 1, "FinishedInsertingShells")
		REVO A 0 A_JumpIfInventory("Kicking",1,"DoKick")  //A_JumpIfInventory("Kicking",1,"DoKickReloading") Too inconsistent and unpolished for this beta,you'll work on this later!
		REVO A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		REVO A 0 A_JumpIfInventory("RevolverAmmo",0,"FinishedInsertingShells")//If there are 8+1 shells, stop it immediately
		Goto InsertingShells

	FinishedInsertingShells:
		REVO A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_TakeInventory("HasUnloadedRevolver", 1)
		REVR QPONMLKJIH 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		TNT1 A 0 A_PLaySound("Revolver/Close", 5)
		REVR GFEEDCBA 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		REVO A 0 A_Takeinventory("Reloading",1)
		REVO A 0 A_JumpIfInventory("NeedToHammer",1,"hammering")
		Goto Ready

	TurboReload:
		SHSS BC 0
	TurboBullets:
		REVO A 0
		REVO A 0 A_JumpIfInventory("RevolverAmmo",0,"FinishTurboReload")
		REVO A 0 A_JumpIfInventory("AmmoShell",1,3)
		Goto FinishTurboReload
		TNT1 AAAA 0
		REVO A 0 A_Giveinventory("RevolverAmmo",1)
		REVO A 0 A_Takeinventory("AmmoShell",1,TIF_NOTAKEINFINITE)
		Goto TurboBullets

	FinishTurboReload:
		REVO A 1 A_Refire
		Goto Ready3

	ReloadUnloaded:
		REVR ABCDEFF 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		TNT1 A 0 A_PLaySound("Revolver/Open", 5)
		REVU ABCDEEFGHIJKKKKKKKK 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		REVU LMN 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("Shotgun/Reload", 5)
	InsertBullets:
		TNT1 AAAA 0
		REVO A 0 A_JumpIfInventory("RevolverAmmo",0,"FinishUnloadedReload")
		REVO A 0 A_JumpIfInventory("AmmoShell",1,3)
		Goto FinishUnloadedReload
		TNT1 AAAAAA 0
		REVO A 0 A_Giveinventory("RevolverAmmo",1)
		REVO A 0 A_Takeinventory("AmmoShell",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets

	FinishUnloadedReload:
		REVU OOPPPPPPPQQR 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		Goto FinishedInsertingShells

	Unload:
		REVO A 0 A_ZoomFactor(1.0)
		REVO A 0 A_Takeinventory("Unloading",1)
		REVO A 0 A_Takeinventory("ADSmode",1)
		REVO A 0 A_Takeinventory("Zoomed",1)
		REVO A 0 A_JumpIfInventory("RevolverAmmo",1,3)
		Goto Ready
		TNT1 AAA 0
		REVO A 0 A_Takeinventory("Zoomed",1)
		REVO A 0 A_GiveInventory ("Pumping", 1)
		REVO A 0 A_Takeinventory("Unloading",1)
		REVR ABCDEFF 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		TNT1 A 0 A_PLaySound("Revolver/Unload", 5)
		REVR GHIJKKK 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		REVR LMNOP 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		REVR Q 2 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
	RemoveBullets:
		TNT1 AAAA 0
		REVO A 0 A_JumpIfInventory("RevolverAmmo",1,1)
		Goto FinishUnload
		TNT1 AAAAAA 0
		REVO A 0 A_Takeinventory("RevolverAmmo",1)
		REVO A 0 A_JumpIfInventory("NoReloading",1,2)
		REVO A 0 A_Giveinventory("AmmoShell",1)
		Goto RemoveBullets

	FinishUnload:
		REVO A 0 A_Giveinventory("NeedToHammer",1)
		REVO A 0 A_Giveinventory("HasUnloadedRevolver",1)
		REVU STKKKKKKJIHGFEDCB 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		REVU A 1 A_PLaySound("Revolver/Close", 5)
		REVR GFEEDCBA 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		REVO A 0 A_Takeinventory("Unloading",1)
		Goto OkToFire

	Steady:
		TNT1 A 1
		Goto Ready

	NoAmmo:
		RIFG A 0
		RIFG A 0 A_ZoomFactor(1.0)
		RIFG A 0 A_Takeinventory("Zoomed",1)
		RIFG A 0 A_Takeinventory("ADSmode",1)
		RIFG A 0 A_JumpIfInventory("TurboReload",1,"TurboReload")
		TNT1 A 0 A_PlaySound("Weapon/Empty", 5)
	NoAmmo2:
		RIFG A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		RIFG A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		RIFG A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		RIFG A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		RIFG A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		RIFG A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		RIFG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		REVO A 5 A_WeaponReady(WRF_ALLOWZOOM | WRF_NOFIRE)
		RIFG A 0 A_JumpIfInventory("Reloading",1,"Reload")
		RIFG A 0 A_JumpIfInventory("TurboReload",1,"TurboReload")
		RIFG A 0 A_JumpIfInventory("RevolverAmmo",1,"Ready3")
		TNT1 A 0 A_JumpIfInventory("NoAutoReload", 1, "NoAmmo2")
		TNT1 A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		RIFG A 0 A_JumpIfInventory("AmmoShell",1,"Reload")
		RIFG A 0 A_JumpIfInventory("FiredPrimary",1,"NoAmmo2")
		Goto Ready3
	}
}

ACTOR RevolverAmmo : Ammo
{
	Inventory.Amount 0
	Inventory.MaxAmount 6
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 6
	Inventory.Icon "REVPA0"
}