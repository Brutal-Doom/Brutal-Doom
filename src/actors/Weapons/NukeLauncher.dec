
ACTOR NukeLauncher : BrutalWeapon// 7452
{
	//$category "Weapons"
	VisibleToPlayerClass BDoomer, Doomer2, TacticalDoomer, Purist
	Inventory.ForbiddenTo ""
	Weapon.AmmoUse 1
	Weapon.AmmoType "Nuke"
	Weapon.SelectionOrder 3000
	+WEAPON.BFG
	+WEAPON.NOAUTOFIRE
	+WEAPON.AMMO_OPTIONAL
	Inventory.PickupSound "BFG/Draw"
	Inventory.PickupMessage "$GOTNUKELAUNCHER"
	Tag "$TAGBD_NUKE"
	Obituary "$OBBD_NUKELAUNCHER"
	States
	{
	Ready:
	Ready3:
	Reload:
		NKLG A 1 A_WeaponReady
		NKLG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		NKLG A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		NKLG A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		NKLG A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		NKLG A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		NKLG A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		Goto Ready3

	StartSprint:
		NKLG A 1 A_WeaponReady(WRF_NOFIRE)
		NKLG A 0 A_Takeinventory("Zoomed",1)
		NKLG A 0 A_Takeinventory("ADSmode",1)
		NKLG A 0 A_ZoomFactor(1.0)
		NKLG A 0 A_JumpIfInventory("UsedStamina", 40, "StopSprintTired")
	Sprinting:
		NKLG A 0 offset(-9,32)
		//NKLG A 0 offset(-9,32) A_JumpIfInventory("IsStandingStill", 1, "Ready3")
		NKLG A 0 offset(-9,32) A_JumpIfInventory("UsedStamina", 100, "StopSprintTired")
		PLAY A 0 offset(-9,32) ACS_ExecuteAlways(852, 0, 0, 0, 0)//Makes player faster.
		NKLG A 0 offset(-9,32) A_JumpIfInventory("PowerStrength", 1, 2)
		NKLG A 0 offset(-9,32) A_GiveInventory("UsedStamina", 15)
		NKLG A 0 offset(-9,32)
		NKLG A 1 offset(-9,34) A_SetPitch(pitch -0.5)
		NKLG A 1 offset(-6,36) A_SetPitch(pitch -0.5)
		NKLG A 1 offset(-3,38) A_SetPitch(pitch -0.5)
		NKLG A 1 offset(0,38) A_SetPitch(pitch -0.5)
		NKLG A 1 offset(3,36) A_SetPitch(pitch -0.5)
		NKLG A 1 offset(6,34) A_SetPitch(pitch -0.5)
		NKLG A 1 offset(9,32) A_SetPitch(pitch -0.5)
		TNT1 C 0 offset(9,32) A_WeaponReady(WRF_NOBOB)
		NKLG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		NKLG A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		NKLG A 0 A_JumpIfInventory("Reloading",1,"Reload")
		NKLG A 0 offset(-9,32) A_GiveInventory("UsedStamina", 8)
		NKLG A 0 offset(9,33) A_SpawnItemEx("FootStep", 0, 0, 2, 0, 0, -4)
		NKLG A 1 offset(9,34) A_SetPitch(pitch +0.5)
		NKLG A 1 offset(6,36) A_SetPitch(pitch +0.5)
		NKLG A 1 offset(3,38) A_SetPitch(pitch +0.5)
		NKLG A 1 offset(0,38) A_SetPitch(pitch +0.5)
		NKLG A 1 offset(-3,36) A_SetPitch(pitch +0.5)
		NKLG A 1 offset(-6,34) A_SetPitch(pitch +0.5)
		NKLG A 1 offset(-9,32) A_SetPitch(pitch +0.5)
		NKLG A 0 offset(-9,32) A_WeaponReady(WRF_NOBOB)
		NKLG A 0 offset(-9,32) A_JumpIfInventory("IsRunning", 1, "Sprinting")
		Goto StopSprint

	StopSprintTired:
		NKLG A 1
		PLAY A 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		NKLG A 0 A_PlaySound("Tired", 2)
		NKLG A 0 A_TakeInventory("UsedStamina", 2)
		NKLG AAAAA 1 A_WeaponReady
		NKLG A 0 A_TakeInventory("UsedStamina", 2)
		NKLG AAAAA 1 A_WeaponReady
		NKLG A 0 A_TakeInventory("UsedStamina", 2)
		NKLG AAAAA 1 A_WeaponReady
		NKLG A 0 A_TakeInventory("UsedStamina", 2)
		NKLG AAAAA 1 A_WeaponReady
		NKLG A 0 A_TakeInventory("UsedStamina", 2)
		NKLG AAAAA 1 A_WeaponReady
		Goto Ready

	StopSprint:
		NKLG A 1
		NKLG A 0 A_JumpIfInventory("UsedStamina", 60, "StopSprintTired")
		PLAY A 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		Goto Ready

	DryFire:
		NKLG A 5
		NKLG A 0 A_PlaySound("Weapon/Empty", 5)
		Goto Ready+6

	Deselect:
		NKLG A 0 A_StopSound(CHAN_WEAPON)
		NKLG A 0 A_TakeInventory("TossGrenade", 1)
		NKLG A 1 A_Lower
		Loop

	Select:
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_GiveInventory("TokenCleaner")
		NKLG A 0 A_Giveinventory("FlameCannonSelected",1)
		TNT1 A 0 A_SetCrosshair( CallACS("GetCrosshair", 21) )
		NKLG AAA 1 A_Raise
		NKLG A 0 A_PlaySound("BFG/Draw", 5)
		TNT1 AAA 0
		TNT1 AAAAAAAAA 0 A_Raise
		Goto SelectAnimation

	SelectAnimation:
		TNT1 A 1
		NKLG A 0 A_GunFlash
		Goto Ready

	Spawn:
		NKLG D 1
		Loop

	Fire:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Nuke", 1, 1)
		Goto Ready3
		TNT1 A 0 A_AlertMonsters
		NKLG B 1 BRIGHT
		NKLG A 0 A_PlaySound ("Rocket/Fire", 1)
		NKLG A 0 A_PlaySound ("brain/spit", 5)
		NKLG A 0 A_ZoomFactor(0.9)
		NKLG B 1 BRIGHT
		NKLG A 0 A_ZoomFactor(1.0)
		NKLG A 0 A_FireCustomMissile("NuclearRocket", 0, 1, 0, -6)
		NKLG C 2 BRIGHT
		NKLG A 1 offset(0,28)
		NKLG A 1 offset(0,24)
		NKLG A 1 offset(0,26)
		NKLG A 1 offset(0,28)
		NKLG A 1 offset(0,30)
		NKLG A 1 offset(0,32)
		Goto Ready+6
	}
}