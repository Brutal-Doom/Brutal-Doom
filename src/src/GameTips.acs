
#pragma raw define on
#pragma raw include on

#include "bcsformat.acs"

using BCSFormat;

private raw r1, r2, r3;

int atoi (str s)
{
    int c, acum = 0, sLen = StrLen(s);

    for(int i = 0; i < sLen; i++)
    {
        c = GetChar(s, i);

        if( (c < '0') || (c > '9') )
            break;

        acum = acum * 10;
        acum = acum + (c - '0');
    }

    return acum;
}

bool GetLanguageKey (str key)
{
    r1 = StrParam(l:key);

    return !!StriCmp(r1, key);
}

Script "BD_GetRandomTip"
{
    static str tipArgs[3];
    str tipKey, tipStr;
    int tipNum = 1;

    SetResultValue((raw)"");

	if(GetCvar("BD_Welcome") == 1)
	{
		SetCvar("BD_Welcome", 0);
		SetResultValue(StrParam(l:"BD_Welcome"));
		terminate;
	}

	if(!GetLanguageKey("BD_TIP_AMOUNT"))
    {
        log(s:r1);
        terminate;
    }
	
    if(!GetLanguageKey("BD_TIP_AMOUNT"))
    {
        log(s:r1);
        terminate;
    }

    tipNum = Random(1, atoi(r1));
    tipKey = StrParam(s:"BD_TIP", i:tipNum);

    if(!GetLanguageKey(tipKey))
    {
        log(s:r1);
        terminate;
    }

    tipStr = r1;

    for(int i = 0; i < tipArgs.length(); i++)
    {
        if(GetLanguageKey(StrParam(s:tipKey, s:"_ARG", i:i)))
            tipArgs[i] = r1;
        else
            tipArgs[i] = "";
    }

	str actualTip = StrFormat(tipStr, tipArgs[0], tipArgs[1], tipArgs[2]);
	
    SetResultValue(StrParam(l:"BD_TIP", i:tipNum, s:": \cj", s:actualTip));
}
